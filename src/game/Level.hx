class Level extends GameChildProcess {
	/** Level grid-based width**/
	public var cWid(default,null): Int;
	/** Level grid-based height **/
	public var cHei(default,null): Int;

	/** Level pixel width**/
	public var pxWid(default,null) : Int;
	/** Level pixel height**/
	public var pxHei(default,null) : Int;

	public var data : World_Level;
	var tilesetSource : h2d.Tile;

	public var marks : dn.MarkerMap<LevelMark>;
	var invalidated = true;

	public function new(ldtkLevel:World.World_Level) {
		super();

		createRootInLayers(Game.ME.scroller, Const.DP_BG);
		data = ldtkLevel;
		cWid = data.l_Collisions.cWid;
		cHei = data.l_Collisions.cHei;
		pxWid = cWid * Const.GRID;
		pxHei = cHei * Const.GRID;
		tilesetSource = hxd.Res.levels.mitosisWorldTiles.toAseprite().toTile();

		marks = new dn.MarkerMap(cWid, cHei);
		for(cy in 0...cHei)
		for(cx in 0...cWid) {
			if( data.l_Collisions.getInt(cx,cy)==1 )
				marks.set(M_Coll_Wall, cx,cy);
		}

		addInvisibleWallCollisions();
	}

	override function onDispose() {
		super.onDispose();
		data = null;
		tilesetSource = null;
		marks.dispose();
		marks = null;
	}

	/** TRUE if given coords are in level bounds **/
	public inline function isValid(cx,cy) return cx>=0 && cx<cWid && cy>=0 && cy<cHei;

	/** Gets the integer ID of a given level grid coord **/
	public inline function coordId(cx,cy) return cx + cy*cWid;

	/** Ask for a level render that will only happen at the end of the current frame. **/
	public inline function invalidate() {
		invalidated = true;
	}

	/** Return TRUE if "Collisions" layer contains a collision value **/
	public inline function hasCollision(cx,cy) : Bool {
		return !isValid(cx,cy) ? true : marks.has(M_Coll_Wall, cx,cy);
	}

	function addInvisibleWallCollisions() {
		var invisibleWalls:Array<Dynamic> = cast Reflect.field(data.l_Entities, "all_InvisibleWall");
		if( invisibleWalls==null )
			return;

		for( wall in invisibleWalls ) {
			var grid = getEntityGrid(wall);
			var wallCx = grid.cx;
			var wallCy = grid.cy;
			var wallCWid = M.imax(1, M.ceil(getEntitySize(wall, "width") / Const.GRID));
			var wallCHei = M.imax(1, M.ceil(getEntitySize(wall, "height") / Const.GRID));

			for( cy in wallCy...wallCy + wallCHei )
				for( cx in wallCx...wallCx + wallCWid )
					if( isValid(cx,cy) )
						marks.set(M_Coll_Wall, cx,cy);
		}
	}

	function getEntityGrid(entity:Dynamic) {
		var cx = readEntityInt(entity, "cx", null);
		var cy = readEntityInt(entity, "cy", null);
		if( cx!=null && cy!=null )
			return { cx:cx, cy:cy };

		var grid:Array<Int> = cast Reflect.field(entity, "__grid");
		return {
			cx: grid!=null && grid.length>0 ? grid[0] : 0,
			cy: grid!=null && grid.length>1 ? grid[1] : 0,
		};
	}

	function getEntitySize(entity:Dynamic, field:String) : Int {
		return readEntityInt(entity, field, Const.GRID);
	}

	function readEntityInt(entity:Dynamic, field:String, defaultValue:Null<Int>) : Null<Int> {
		var value = Reflect.field(entity, field);
		return value==null ? defaultValue : Std.int(value);
	}

	/** Render current level**/
	function render() {
		root.removeChildren();

		var collisionTiles = new h2d.TileGroup(tilesetSource, root);
		data.l_Collisions.render(collisionTiles);

		var decorationTiles = new h2d.TileGroup(tilesetSource, root);
		data.l_DecorationTiles.render(decorationTiles);
	}

	override function postUpdate() {
		super.postUpdate();

		if( invalidated ) {
			invalidated = false;
			render();
		}
	}
}